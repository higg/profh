 {ctl}←{fnname}Graph ctl;file;tie;tmpl;arg;md;ix;dir;i
 ⍝ the timings in SpeedScope are displayed in microseconds
 dir←Init''
 :If ctl≡⍬
     ctl←1 2 3 5 ⎕PROFILE'tree'
 :EndIf
 :If 0≠⎕NC'fnname'
     ix←0⌈¯1+⊃#.WHERE(2⊃ctl)≢¨⊂fnname
     ctl←ix↓¨ctl
     md←0⌈¯1+(⊃ctl)[1]
 :Else
     :If 0≠i←+/∧\0=4⊃ctl
         md←i⊃⊃ctl
     :Else
         md←0
     :EndIf
 :EndIf

 tmpl←#._OS.JoinPath(#._OS.GetTempPath ⍬)'speedscope_.js'
 tie←tmpl ⎕NCREATE ⎕OPT'Unique' 1⊢0
 file←⊃0 ⎕NINFO tie
 ⎕NUNTIE tie

 file 1 md SpeedScope ctl

 arg←#.uTC'file:///⍟1/index.html#localProfilePath=⍟0'file dir
 #._OS.ShellExecute'msedge.exe'arg

⍝ ↓↓↓ Much heuristic, very wow.
 ⎕DL 2+(⊃2 ⎕NINFO file)÷10000000
 3 ⎕NDELETE file
⍝∘ (c)APLIT D PRF stf 24/10/2022 12:13          ¯1096400100 C01
