{file}←file SpeedScope(depth fnname line elapsed);stk;ld;fmt;l;s;⎕PP;d;f;e;_out
    _out←⎕NEW Writer(file 1)
    ⍞←'⍝ '
    ⎕PP←18
    stk←⍬
    ld←¯1000
    fmt←{
        (¯1↓↑,/⌽⍵,¨';'),' ',⍕⍺ ⍝ //! Was vtol
    }

    elapsed←0⌈⌈1000000×elapsed ⍝ The timings in SpeedScope are displayed in microseconds //! Is that right?

    :For (d f l e) :InEach depth fnname line elapsed
        f,←(¯1≠l)/'[',(⍕l),']'          ⍝ Compose function[line] (omit line if whole-function entry)

        :If d>ld
            stk,⍨←⊂f
        :ElseIf d<ld
            stk←(1+0.5×ld-d)↓stk
            stk,⍨←⊂f
        :Else
            ⍝59  #.Conga.LIB.Send                          4     377       7.371103391051292          35.96187336370349         1131      3770
            ⍝60  #.Conga.⍙CallRL                                 377      26.184714399278164          26.184714399278164         377       377
            ⍝60  #.Conga.LIB.check                               377       2.406055573374033           2.406055573374033        2262      2262
            ⍝61  #.Conga.LIB.check                         0     377       0.3276783972978592          0.3276783972978592        377       377
            (⊃stk)←f
        :EndIf

        :If e>0
        :AndIf _out.Write e fmt stk     ⍝ Write partial
            ⍞←'○'
        :EndIf

    ld←d
    :EndFor

    :If _out.Flush 1
        ⍞←'file probably too big'
    :EndIf

    ⍞←'∘'
    _out.Close ⍬
