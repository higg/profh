 {ctl}←{fnname}Graph ctl;p;del;file;tie;arg;md;ix;i
 ⍝ the timings in SpeedScope are displayed in microseconds

 ⍝ //! Hoist this, I think
 ctl←{⍵≢⍬:⍵ ⋄ 1 2 3 5⎕PROFILE'tree'}ctl ⍝ Fetch profile results, unless provided
 ⍝ ctl ← 1: depth level  2: function name  3: function line number  4: exclusive accumulated time (ms)

 :If 0=≢⊃ctl                            ⍝ Abort if no profile data available
    ⎕←'No profile data!'
    :Return
 :EndIf


 ⍝
 ⍝ Determine if call stack pruning heuristic is to be used. //! Flesh out details
 ⍝
 :If 0≠⎕NC'fnname' ⍝ If `fnname` defined, use it for results pruning //! Make explicit
     ix←0⌈¯1+⊃⍸(2⊃ctl)≢¨⊂fnname ⍝ (I)nde(x) of function's first occurence
     ctl←ix↓¨ctl                ⍝ Remove entries before it
     md←0⌈¯1+(⊃ctl)[1]          ⍝ //! ???
     ⍝ fnname should be rationalized against '#.' removing heuristic
 :Else
     :If 0≠i←+/∧\0=4⊃ctl ⍝ //! Figure this one out (prunes leading 0's)
         md←i⊃⊃ctl
     :Else
         md←0
     :EndIf
 :EndIf

 ⍝
 ⍝ Delete previous temporary export files, unless directed otherwise.
 ⍝
 del←~Config.Curr.keepTemp              ⍝ Decide if deleting
 3 ⎕MKDIR TEMP_DIR                      ⍝ Create temporary directory, if needed
 p←⊃⎕NINFO ⎕OPT 1⊢TEMP_DIR,'/speedscope_*.js' ⍝ Identify previous export files

 ⎕←(⊃(1+del)⌷'Retaining ' 'Deleting '),(⍕≢p),' previous export file',ps ≢p

 {}3 (⎕NDELETE⍣del) p                   ⍝ Delete previous files, if so configured; suppress output

 ⍝
 ⍝ Create temporary export file.
 ⍝
 3 ⎕MKDIR TEMP_DIR                      ⍝ Create temporary directory, if needed
 file←TEMP_DIR,'/speedscope_.js'        ⍝ Determine temporary file location and rootname
 tie←file(⎕NCREATE ⎕OPT'Unique' 1)0     ⍝ Create file, appending random chars to filename
 file←⊃0 ⎕NINFO tie                     ⍝ Pick up randomized filename
 ⎕NUNTIE tie

 file 1 md SpeedScope ctl               ⍝ Perform export

 ⍝
 ⍝ Launch speedscope in browser.
 ⍝
 arg←'file:///',Config.Curr.libDir,'/speedscope/index.html#localProfilePath=',file
 Browser.browse arg
