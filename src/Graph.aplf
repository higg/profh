{ctl}←Graph w;arg;cfg;del;file;ix;md;p;tfn;tie;ct;h

    cfg←Config.Curr                     ⍝ Alias effective configuration

    w←{⍵≢⍬:⍵ ⋄ 1 2 3 5⎕PROFILE'tree'}w  ⍝ Fetch profile results, unless provided

    :If 0=≢⊃w                           ⍝ Abort if no profile data available
        ⎕←'No profile data!'
        :Return
    :EndIf

    ct←⍉↑w                              ⍝ Convert to call tree matrix

    ⍝
    ⍝ At this point,  ct  is the call tree matrix with columns as follows:
    ⍝   [1] depth level
    ⍝   [2] function name
    ⍝   [3] function line number -- note that whole-function entries are ¯1, not ⍬
    ⍝   [4] exclusive accumulated time (ms)
    ⍝

    ⍝
    ⍝ Apply call tree pruning heuristics.
    ⍝
    ⍝ Reasons why pruning the call tree may be advantageious include:
    ⍝   - Focusing on areas of particular interest to the user.
    ⍝   - Reducing the size of the export file. Speedscope has been observed
    ⍝     to fail on files larger than ~500MB, and since the function names of
    ⍝     the entire stack are repeated for every leaf node entry (and we're
    ⍝     doing line-level!), eliminating uninteresting areas of the tree (e.g. a
    ⍝     common root) can have a huge impact.
    ⍝   - Eliminating any contributions of this tool apparent when profiling
    ⍝     was initiated under our aegis.
    ⍝
    md←0                                ⍝ Assume no depth-based pruning

    :If 0<≢tfn←cfg.topFn                ⍝ If function-level pruning requested...
        tfn←'#.',⍣(⌊/'#⎕'≠⊃tfn)⊢tfn     ⍝ Assume root NS if not explicit
        ix←0⌈¯1+⊃⍸ct[;2]≡¨⊂tfn          ⍝ (I)nde(x) of function's first occurence
        ct↓⍨←ix                         ⍝ Remove entries before it
        md←0⌈⊃ct                        ⍝ Record depth of new starting point, to adjust later
        ⎕←'Pruning outside of ',tfn
    :Else
        ⍝
        ⍝ If no explicit pruning point was requested, we will attempt to remove
        ⍝ any entries that were on the stack when profiling started. The
        ⍝ heuristic used to identify those is an own-elapsed time of zero at the
        ⍝ very root of the call tree.
        ⍝
        ix←1++/∧\0=ct[;4]               ⍝ (I)nde(x) of first entry with non-zero elaspsed time
        md←ct[ix;1]                     ⍝ Record depth of new starting point, to adjust later
        ⎕←'Removing leading entries'
    :EndIf

    ⍝
    ⍝ Perform depth-based pruning.
    ⍝
    :If md≠0                            ⍝ Skip if no-op (for performance)
        ct⌿⍨←ct[;1]≥md                  ⍝ Eliminate entries of less depth
        ct[;1]-←md                      ⍝ Adjust depths
        ⎕←'Applying depth updates: ',⍕md
    :EndIf

    ct↑⍨←1++/∧\0≠1↓ct[;1] ⍝ //! Prune after a non-initial depth-0 //! why? -- to clean up other half of stack isolation

    ⍝ //! Revisit above with goal of restructing pruning to within a single top-level branch

    ⍝
    ⍝ Remove (most) whole-function header entries. We will work with line-level
    ⍝ data.
    ⍝
    ⍝ In some circumstances ⎕PROFILE will provide whole-function entries that
    ⍝ aren't followed by line-level entries. In this author's experience, this
    ⍝ behaviour occurs in multi-threaded scenarios where accrued time costs are
    ⍝ implausible; thus undermining the value of the collected data. But others
    ⍝ have had more promising experiences, so we will retain these entries and
    ⍝ include them in the export.
    ⍝
    h←~2|ct[;1]                         ⍝ Create mask of function header entries
    ct[⍸h;1]+←1                         ⍝ Increase header entry depth for consistency with peers (relevant to retained headers)
    h∧←1,⍨2≡/ct[;2]                     ⍝ Exclude anomalous singleton header entries from purging
    ct⌿⍨←~h                             ⍝ Purge normal headers

    ct[;2]←'#.'trimPfx ct[;2]           ⍝ Trim root namespace indicators
    ct[;1]÷←2                           ⍝ Adjust depths to increment by 1

    ⍝
    ⍝ Delete previous temporary export files, unless directed otherwise.
    ⍝
    del←~cfg.keepTemp                   ⍝ Decide if deleting
    3 ⎕MKDIR TEMP_DIR                   ⍝ Create temporary directory, if needed
    p←⊃⎕NINFO ⎕OPT 1⊢TEMP_DIR,'/speedscope_*.js' ⍝ Identify previous export files

    ⎕←('Retaining ' 'Deleting '⊃⍨1+del),(⍕≢p),' previous export file',ps≢p

    {} 3 ⎕NDELETE⍣del⊢ p                ⍝ Delete previous files, if so configured; suppress output

    ⍝
    ⍝ Create temporary export file.
    ⍝
    3 ⎕MKDIR TEMP_DIR                   ⍝ Create temporary directory, if needed
    file←TEMP_DIR,'/speedscope_.js'     ⍝ Determine temporary file location and rootname
    tie←file ⎕NCREATE ⎕OPT'Unique'1 ⊢0  ⍝ Create file, appending random chars to filename
    file←⊃0 ⎕NINFO tie                  ⍝ Pick up randomized filename
    ⎕NUNTIE tie

    file SpeedScope ↓⍉ct                ⍝ Perform export

    ⍝
    ⍝ Launch speedscope in browser.
    ⍝
    arg←'file:///',cfg.libDir,'/speedscope/index.html#localProfilePath=',file
    Browser.browse arg
