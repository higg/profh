{ctl}←Graph ctl;arg;cfg;del;file;i;ix;md;minimum;p;tfn;tie;depth;fnname;line;elapsed

    cfg←Config.Curr                     ⍝ Alias effective configuration

    ctl←{⍵≢⍬:⍵ ⋄ 1 2 3 5⎕PROFILE'tree'}ctl ⍝ Fetch profile results, unless provided
    :If 0=≢⊃ctl                            ⍝ Abort if no profile data available
        ⎕←'No profile data!'
        :Return
    :EndIf

    ⍝
    ⍝ At this point, ctl is a list of vectors composed as follows:
    ⍝   [1] depth level
    ⍝   [2] function name
    ⍝   [3] function line number
    ⍝   [4] exclusive accumulated time (ms)
    ⍝

    ⍝
    ⍝ Determine if call stack pruning heuristic is to be used. //! Flesh out details
    ⍝
    md←0                                ⍝ Assume no depth-pruning

    :If 0<≢tfn←cfg.topFn                ⍝ If function-level pruning requested...
        tfn←'#.',⍣(⌊/'#⎕'≠⊃tfn)⊢tfn     ⍝ Assume root NS if not explicit
        ix←0⌈¯1+⊃⍸(2⊃ctl)≡¨⊂tfn         ⍝ (I)nde(x) of function's first occurence
        ctl←ix↓¨ctl                     ⍝ Remove entries before it
        md←0⌈¯1+⊃⊃ctl                   ⍝ Record depth of new starting point, to adjust later
        ⎕←'Pruning outside of ',tfn
    :ElseIf 0<i←+/∧\0=4⊃ctl ⍝ //! Figure this one out (prunes leading 0's)
        ⍝ Remove entries that were on the stack when profiling started (exlusive time 0)//!
        ⍝ Mention to SL //!
        md←i⊃⊃ctl
        ⎕←'Removing leading entries'
    :EndIf


    ⍝ //! Rough application of min-depth

    depth fnname line elapsed←ctl ⍝ //! Also remove localizations

    ⍝
    ⍝ minimum depth: remove the crud to make the file smaller
    ⍝ //! Make md be the minimum depth to retain, not prune (else rename)
    ⍝
    :If md≠0
        depth fnname line elapsed⌿⍨←⊂depth>md ⍝ //! ≥ (but need to adjust caller)
        depth-←md+1 ⍝ no +1
    :EndIf

    depth fnname line elapsed↑⍨←1++/∧\0≠1↓depth ⍝ //! Prune after a non-initial depth-0
    fnname trimPfx⍨←'#.'                ⍝ Trim root namespace indicators
    depth fnname line elapsed⌿⍨←⊂(1=2|depth)∨(line=¯1)∧1,⍨2≢/fnname ⍝ Remove top level entries //! Even depths, or ¯1 lines even when function doesn't change (probably for some stored format)
    depth+←line=¯1                      ⍝ Convert ¯1's to 0's

    ctl←depth fnname line elapsed
    ⍝ //!

    ⍝
    ⍝ Delete previous temporary export files, unless directed otherwise.
    ⍝
    del←~cfg.keepTemp                   ⍝ Decide if deleting
    3 ⎕MKDIR TEMP_DIR                   ⍝ Create temporary directory, if needed
    p←⊃⎕NINFO ⎕OPT 1⊢TEMP_DIR,'/speedscope_*.js' ⍝ Identify previous export files

    ⎕←('Retaining ' 'Deleting '⊃⍨1+del),(⍕≢p),' previous export file',ps≢p

    {}3⎕NDELETE⍣del⊢p                   ⍝ Delete previous files, if so configured; suppress output

    ⍝
    ⍝ Create temporary export file.
    ⍝
    3 ⎕MKDIR TEMP_DIR                   ⍝ Create temporary directory, if needed
    file←TEMP_DIR,'/speedscope_.js'     ⍝ Determine temporary file location and rootname
    tie←file ⎕NCREATE ⎕OPT'Unique'1 ⊢0  ⍝ Create file, appending random chars to filename
    file←⊃0 ⎕NINFO tie                  ⍝ Pick up randomized filename
    ⎕NUNTIE tie

    file 1 md SpeedScope ctl            ⍝ Perform export

    ⍝
    ⍝ Launch speedscope in browser.
    ⍝
    arg←'file:///',cfg.libDir,'/speedscope/index.html#localProfilePath=',file
    Browser.browse arg
