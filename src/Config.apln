:Namespace Config

    ⍝
    ⍝ Default configuration settings.
    ⍝
    Default←⎕NS⍬
    Default.browser         ←''         ⍝ Browser launched for web-based applications (HtmlRenderer if empty)
    Default.cpu             ←0          ⍝ Launch ⎕PROFILE in cpu mode, else elapsed
    Default.libDir          ←''         ⍝ Directory housing external tools/libraries (e.g. speedscope), set later
    Default.keepTemp        ←0          ⍝ Indicates if previous temporary files should be kept rather than deleted
    Default.showCmd         ←0          ⍝ If set, displays external command before execution
    Default.zoom            ←0          ⍝ HtmlRenderer zoom level
    Default.topFn           ←''         ⍝ Explicit top-level function (prune above)
    Default.msScale         ←1000       ⍝ Scaling factor applied to millisecond timing values for presentation

    Curr←Default                        ⍝ Holds configuration used at runtime
    srcDir←''                           ⍝ Package source directory (assigned later)

    apply←{                             ⍝ Resovles defaults and applies incomings ucmd modifiers into Config.Curr 
        c←⎕NS Default                   ⍝ Start with default configuration
        projd←##.updir srcDir           ⍝ Determine project location
        c.libDir←projd,'/lib'           ⍝ Cache directory containing external tools
        c←c ##.update fetchLocal projd  ⍝ Fetch and apply local overrides, if any
        c←c applyModifiers ⍵            ⍝ Apply incoming modifiers
        ⎕THIS.Curr←c                    ⍝ Cache in global context
        ⍵                               ⍝ Return right, for caller's convenience
    }

    fetchLocal←{
        n←⍵,'-cfg/ConfigLocal.json'     ⍝ Determine file path
        e←⎕NEXISTS n                    ⍝ Check its existence
        s←(1+e)⊃'{}',⍥⊂⊃⎕NGET⍣e⊢n 0     ⍝ Fetch config, or default to empty map
        ⎕JSON s                         ⍝ Convert to JSON
    }

    applyModifiers←{                    ⍝ Applies overriding ucmd modifiers to given namespace
        f←##.keys ⍺                     ⍝ Get field names
        v←(⍺⍎¨f)⍵.Switch¨f              ⍝ Resolve values
        _←f ⍺.{⍎⍺,'←⍵'}¨v               ⍝ Apply updates
        ⍺                               ⍝ Return left, for caller's convenience
    }

:EndNamespace
