:Namespace Utils
    ##.out←{⍞←(×≢⍵)/⍵,⎕UCS 10}          ⍝ Emits given string to output, unless empty

    trimPfx←{                           ⍝ Trims prefix from strings
        0≡≢⍺ : ⍵                        ⍝ Skip the trivial case
        l←≢⍺                            ⍝ Prefix (l)ength
        (l∘↓¨@(⍸↑∧/⍺=¨(⍳l)⌷[2]¨⊂↑⍵))⍵   ⍝ Find matches and trim
    }
    ##.trimPfx←trimPfx

    update←{                            ⍝ Updates intersecting fields from dict ⍵ to ⍺
        i←⊃∩/{keys ⍵}¨⍺ ⍵               ⍝ Find intersecting fields
        0=≢i: ⍺                         ⍝ Abort if no updates to be made
        _←⍺⍎¨'←⍵.'∘{⍵,⍺,⍵}¨i            ⍝ Perform update
        ⍺                               ⍝ Return modified NS ref, for convenience
    }
    ##.update←update

    updir←{                             ⍝ Returns parent of given /-separated directory path
        ⍺←1                             ⍝ Default to up one level
        i←⍸'/'=⍵                        ⍝ Indices of separators
        cp←¯1+(1+⍺⌊≢i)⊃⌽i,≢⍵            ⍝ Choose best cut point, including none
        cp⌈←⊃i                          ⍝ Keep at least one separator (stay grounded in tree)
        cp↑⍵                            ⍝ Perform cut
    }
    ##.updir←updir

    keys←{⍺←⍳9 ⋄ vtrim¨↓⍵.⎕NL⍺}         ⍝ Returns names in given namespace (⍵) of given class (⍺) if provided, else all
    ##.keys←keys

    ⍝
    ⍝ Pluralization helpers.
    ⍝
    _plural←{(⊃⍣(~≢⍴⍵))⍺⌷⍨⊂1+1≠⍵}       ⍝ Indexes into singular/plural suffices (⍺) by oridnalities (⍵)
    ##.ps←''(,'s')∘_plural              ⍝ Returns default English pluralization suffix string based on input count (⍵)

    ⍝
    ⍝ --- Lifted from dfns workspace ------------------------------------------
    ⍝

    vtrim←{⎕IO←0                    ⍝ Trim trailing blanks from line-vector.
        lf sp←⎕UCS 13 32            ⍝ linefeed and space characters.
        1↓¯1↓{                      ⍝ without linefeeds brackets,
            types←lf sp⍳⍵           ⍝ 0:lf, 1:sp, 2:ch.
            mask←~1 1⍷types         ⍝ mask of non-(duplicate blanks).
            comp←mask/types         ⍝ ignoring duplicate blanks,
            csl←2 1 0⍷comp          ⍝ sequence: ch, sp, ··· sp, lf.
            lsl←0 1 0⍷comp          ⍝ sequence: lf, sp, ··· sp, lf.
            from←mask\csl∨lsl       ⍝ sequence: xx, sp, ··· sp, lf.
            upto←1 0⍷types          ⍝ sequence: ··········· sp, lf.
            (~¯1⌽≠\from∨upto)/⍵     ⍝ compressed out: <sp, ··· sp>, lf.
        }lf,⍵,lf                    ⍝ bracketed with linefeeds chars.
    }

    ##.vtrim←vtrim

    vtol←{          ⍝ Nested vector to lines.
        ⍺←⎕UCS 13   ⍝ default separator: new-line.
        ↑,/⍵,¨⊂⍺    ⍝ ⍺-separated vector items.
    }
    ##.vtol←vtol


:EndNamespace
